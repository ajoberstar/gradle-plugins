buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group:'de.huxhorn.gradle', name:'de.huxhorn.gradle.pgp-plugin', version:'0.0.3'
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: de.huxhorn.gradle.pgp.PgpPlugin

group = 'org.ajoberstar'
name = 'gradle-plugins'
version = '0.1.0.SNAPSHOT'
description = 'A set of gradle plugins for popular tools and other assorted plugins.'

configurations {
	integTestCompile {
		extendsFrom testCompile
	}
	integTestRuntime {
		extendsFrom integTestCompile, testRuntime
	}
}

sourceSets {
	integTest {
		java.srcDir file('src/integTest/java')
		resources.srcDir file('src/integTest/resources')
		groovy.srcDir file('src/integTest/groovy')
		compileClasspath = sourceSets.main.classes + sourceSets.test.classes + configurations.integTestCompile
		runtimeClasspath = classes + compileClasspath + configurations.integTestRuntime
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile(
		gradleApi()
	)
	
	groovy(
		gradleApi()
	)
	
	testCompile(
		[group:'junit', name:'junit', version:'4.8.2']
	)
}

task integTest(type: Test, dependsOn: jar) {
	testClassesDir = sourceSets.integTest.classesDir
	classpath = sourceSets.integTest.runtimeClasspath
	//systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn integTest

task sourcesJar(type:Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type:Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories.mavenDeployer {
		name = 'Sonatype OSS'
		repository(url:'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		snapshotRepository(url:'https://oss.sonatype.org/content/repositories/snapshots') {
			authentication(userName:sonatypeUserName, password:sonatypePassword)
		}
		
		pom.project {
			name name
			description description
			url 'http://github.com/ajoberstar/gradle-plugins'
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0'
				}
			}
			developers {
				developer {
					id 'ajoberstar'
					name 'Andrew Oberstar'
					email 'andrew@ajoberstar.org'
				}
			}
			scm {
				connection 'git://github.com/ajoberstar/gradle-plugins.git'
				developerConnection 'git@github.com:ajoberstar/gradle-plugins.git'
				url 'http://github.com/ajoberstar/gradle-plugins'
			}
		}
	}
}
